plugins {
  id "java-library"
  id "com.github.spotbugs" version "5.0.12" apply false
  id "org.sonarqube" version "3.4.0.2513"
}

wrapper {
    gradleVersion = "7.5.1"
}

dependencies {
  implementation project(":application")
  implementation project(":console")
  implementation project(":core")
}

group "com.mower"
version "1.0.0"

jar {
  manifest {
    attributes("Main-Class": "com.mower.application.MowerApp")
  }

  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }

  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

subprojects {
  repositories {
    mavenCentral()
  }

  apply plugin: "java"
  apply plugin: "jacoco"
  apply plugin: "pmd"
  apply plugin: "com.github.spotbugs"

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(17)
    }
  }

  dependencies {
    testImplementation "org.assertj:assertj-core:3.23.1"
    testImplementation "org.mockito:mockito-junit-jupiter:4.8.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.1"
  }

  test {
    useJUnitPlatform()
  }

  check.finalizedBy(jacocoTestReport)

  pmd {
    ignoreFailures = true
  }
  pmdMain {
    reports {
      xml.required = true
      html.required = true
    }
  }
  pmdTest {
    reports {
      xml.required = true
      html.required = true
    }
  }

  spotbugs {
    ignoreFailures = true
  }
  spotbugsMain {
    reports {
      xml.required = true
      html.required = true
    }
  }
  spotbugsTest {
    reports {
      xml.required = true
      html.required = true
    }
  }

  jacocoTestReport {
    reports {
      xml.required = true
      html.required = true
    }
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "javintx_mower"
    property "sonar.organization", "javintx"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

