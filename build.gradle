plugins {
    id "java-library"
    id "com.github.spotbugs" version "4.7.1" apply false
}

wrapper {
    gradleVersion = "7.1.1"
}

dependencies {
    implementation project(":app")
    implementation project(":core")
}

jar {
    manifest {
        attributes("Main-Class": "com.mower.MowerApp")
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

allprojects {
    group "com.mower"
    version "1.0.0"
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "pmd"
    apply plugin: "com.github.spotbugs"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    dependencies {
        testImplementation "org.assertj:assertj-core:3.19.0"
        testImplementation "org.mockito:mockito-junit-jupiter:3.9.0"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
    }

    test {
        useJUnitPlatform()
    }

    check.finalizedBy(jacocoTestReport)

    pmd {
        ignoreFailures = true
    }
    pmdMain {
        reports {
            xml.required = false
            html.required = true
        }
    }
    pmdTest {
        reports {
            xml.required = false
            html.required = true
        }
    }

    spotbugs {
        ignoreFailures = true
    }
    spotbugsMain {
        reports {
            xml.required = false
            html.required = true
        }
    }
    spotbugsTest {
        reports {
            xml.required = false
            html.required = true
        }
    }
}
